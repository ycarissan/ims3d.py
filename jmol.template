# Jmol online doc: https://chemapps.stolaf.edu/jmol/docs/
#
# File: jmol_commands
# Content: contains a list of jmol commands to plot isocontours within the xy plane from the c4h4_s0.cube file
# Run this script commnands in the Jmol console using: script "jmol_commands"
#
# Set the background color to "lightgray"
set backgroundColor "lightgray"
#
# Load the c4h4_s0.cube file to display the geometry
load "c4h4_s0.cube"
#
# Tune the size of van der Waals radii of atoms to 5%. To totally remove spheres, use: spacefill off
spacefill 5%
#
# Tune the thickness of bonds
wireframe 5
#
# Color all atoms in black
color atoms black
#
# Generate an isosurface stored in an object called c4h4_surface which is a Jmol "isosurface" type and read from the "c4h4_s0.cube" file
isosurface c4h4_surface color range -75 75 colorscheme "rwb" "c4h4_s0.cube" plane xy map "c4h4_s0.cube"
#
# Additional comments:
# color range -75 75: generates a continuous variation of colors from isovalues -75 to 75
# colorscheme "rwb": to have red/blue color mapping
# plane xy map "c4h4_s0.cube": this is the magic command to display the contours within the xy plane using 3D values from the "c4h4_s0.cube" file
# to show/hide the c4h4_surface, just type: "c4h4_surface on" or "c4h4_surface off"
# you can simultaneously show different isosurfaces
# 
# Generate a new isosurface to plot only contour lines of the same surface
isosurface c4h4_contours color range -75 75 colorscheme "rwb" contour increment {-45,75,5} "c4h4_s0.cube" plane xy map "c4h4_s0.cube"
#
# Additional comments:
# contour increment {-45,25,5}: plot contours from value -45 to value 25 every 5
#
# use only red and blue contours 
color isosurface phase red blue
#
# Export image in a png file
write "c4h4_s0.png" as PNGJ
